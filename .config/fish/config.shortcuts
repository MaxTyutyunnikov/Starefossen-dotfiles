#!/usr/bin/fish

# Basic shortcuts
alias l="ls"

switch (uname)
  case Darwin
    alias ls="ls -lG"
  case '*'
    alias ls="ls -l --group-directories-first --color=always"
end

alias mv="mv -v"
alias cp="cp -v"
alias rm="rm -v"
alias ssh="ssh -v"
alias cal="cal -m"

function pgrep -d "more info for pgrep"
  set pidlist (command pgrep -d, -x $argv)
  if test -n "$pidlist"
    ps -fp $pidlist
  end
end

function mkcd -d "mkdir AND cd to it in one go!"
  mkdir -p $argv
  if test $status = 0
    cd $argv
  end
end

function keyhashes -d "View server key-hashes"
  for key in (command ls /etc/ssh/ssh_*_key.pub)
    ssh-keygen -lf $key
  end
end

function noprompt -d "Disable the prompt"
  function fish_prompt
    echo "> "
  end
end

# Release
function release -d "Release new package version to GIT"
  # npm packages
  if test -e package.json
    if test -z $argv[1]
      cat package.json | grep '"version"' | awk '{print substr($2, 2, length($2)-3)}'
      return 0
    else
      set file 'package.json'
      set from '"version": "[^"]+"'
      set to   "\"version\": \"$argv[1]\""
    end

  # wercker packages
  else if test -e wercker-step.yml
    if test -z $argv[1]
      cat wercker-step.yml | grep "version:" | awk '{print $2}'
      return 0
    else
      set file 'wercker-step.yml'
      set from 'version: [^$]+'
      set to   "version: $argv[1]"
    end

  # unknown packages
  else
    echo "No package.json found"
    return 1
  end

  sed -i '' -E "s/$from/$to/g" $file
  git commit -m "Tag release v$argv[1]" $file
  git tag -a "v$argv[1]" -m "v$argv[1]"
end


# NPM / Node
alias npm="docker-compose run --rm dev npm"
alias node="docker-compose run --rm dev node"

# Sherpa
alias sherpa="docker-compose run --rm sherpa ./manage.py"

# Docker
alias dclean="docker rm (docker ps -aqf status=exited)"
alias dcrmf="docker-compose stop -t 0; docker-compose rm -f"

# Applications
function top -d "Use htop, not top"; htop; end
function netstat -d "Netstat with some commonly used options"
  command sudo netstat --numeric --inet -p $argv
end
function easy_install
  echo "pip installs packages. Python packages. An easy_install replacement."
end


# Other stuff
function servedir -d "Serve files of cwd"
  ifconfig | grep "inet addr"
  python -m SimpleHTTPServer
end

function checkip -d "Print this machines external IP address"
  wget http://checkip.dyndns.org/ -O - -o /dev/null | cut -d\  -f 6 | cut -d\< -f 1;
end

function sendkey -d "Send ssh public key to some remote host (ssh-copy-id alternative)"
  if test -f ~/.ssh/id_rsa.pub
    if test (count $argv) -lt 2
      set port 22
    else
      set port $argv[2]
    end
    if test (count $argv) -gt 0
      ssh $argv[1] -p $port "cat >> ~/.ssh/authorized_keys" < ~/.ssh/id_rsa.pub
    end
  else
    echo "There is no ~/.ssh/id_rsa.pub, please generate your keys with 'ssh-keygen'"
  end
end

function prettyJson -d "Pretty-print json (/usr/bin/prettify_json.rb works fine too)"
  echo "Use /usr/local/bin/pjson"
end

#function prettyJson -d "Pretty-print json (/usr/bin/prettify_json.rb works fine too)"
#  python -m json.tool
#end

function man
  if test "$argv[1]" = "lsof"
    echo http://danielmiessler.com/study/lsof/
  else
    command man $argv
  end
end

function ff -d "Find string in current working directory using mdfind"
  if test (count $argv) -lt 2
    set location .
  else
    set location $argv[2]
  end

  for file in (mdfind -onlyin "$location" "$argv[1]")
    echo \n;
    echo "$file";
    echo \n;

    grep -in --color -A 5 -B 5 "$argv[1]" "$file"
  end
end
