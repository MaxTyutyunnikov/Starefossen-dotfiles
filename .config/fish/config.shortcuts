#!/usr/bin/fish

# Basic shortcuts
alias l="ls"

switch (uname)
  case Darwin
    alias ls="ls -lG"
  case '*'
    alias ls="ls -l --group-directories-first --color=always"
end

alias mv="mv -v"
alias cp="cp -v"
alias rm="rm -v"
alias ssh="ssh -v"
alias cal="cal -m"

function pgrep -d "more info for pgrep"
  set pidlist (command pgrep -d, -x $argv)
  if test -n "$pidlist"
    ps -fp $pidlist
  end
end

function mkcd -d "mkdir AND cd to it in one go!"
  mkdir -p $argv
  if test $status = 0
    cd $argv
  end
end

function keyhashes -d "View server key-hashes"
  for key in (command ls /etc/ssh/ssh_*_key.pub)
    ssh-keygen -lf $key
  end
end


# Release
function release -d "Release new package version to GIT"
  if test -e package.json
    if test -z $argv[1]
      cat package.json | grep version
    else
      set from '"version": "[^"]+"'
      set to   "\"version\": \"$argv[1]\""
      sed -i '' -E "s/$from/$to/g" package.json

      git commit -m "Tag release v$argv[1]" package.json
      git tag -a "v$argv[1]" -m "v$argv[1]"
    end
  else
    echo "No package.json found"
  end
end


# Vagrant
function vus -d "Vagrant up, ssh"; vagrant up; vagrant ssh; end
function vdus -d "Vagrant destroy, up, ssh"; vagrant destroy; vagrant up; vagrant ssh; end


# Applications
function top -d "Use htop, not top"; htop; end
function netstat -d "Netstat with some commonly used options"
  command sudo netstat --numeric --inet -p $argv
end
function easy_install
  echo "pip installs packages. Python packages. An easy_install replacement."
end


# Other stuff
function servedir -d "Serve files of cwd"
  ifconfig | grep "inet addr"
  python -m SimpleHTTPServer
end

function checkip -d "Print this machines external IP address"
  wget http://checkip.dyndns.org/ -O - -o /dev/null | cut -d\  -f 6 | cut -d\< -f 1;
end

function sendkey -d "Send ssh public key to some remote host (ssh-copy-id alternative)"
  if test -f ~/.ssh/id_rsa.pub
    if test (count $argv) -lt 2
      set port 22
    else
      set port $argv[2]
    end
    if test (count $argv) -gt 0
      ssh $argv[1] -p $port "cat >> ~/.ssh/authorized_keys" < ~/.ssh/id_rsa.pub
    end
  else
    echo "There is no ~/.ssh/id_rsa.pub, please generate your keys with 'ssh-keygen'"
  end
end

function prettyJson -d "Pretty-print json (/usr/bin/prettify_json.rb works fine too)"
  echo "Use /usr/local/bin/pjson"
end

#function prettyJson -d "Pretty-print json (/usr/bin/prettify_json.rb works fine too)"
#  python -m json.tool
#end

function man
  if test "$argv[1]" = "lsof"
    echo http://danielmiessler.com/study/lsof/
  else
    command man $argv
  end
end

